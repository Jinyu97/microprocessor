	PROCESSOR	16F876A
	INCLUDE <P16F876A.inc>

W_TEMP			EQU	20H	;for interrupt
STATUS_TEMP		EQU	21H
PCLATH_TEMP		EQU	22H

INT_CNT			EQU	25H	;interrupt counter
INT_EN			EQU	26H	;for STW counter

BTNDEL			EQU	27H	;btn 입력 딜레이
BTNVALID		EQU	28H	;확인된 버튼입력 기억

DISP_BUF_1		EQU	2EH
DISP_BUF_2		EQU	2FH
DISP_BUF_3		EQU	30H
DISP_BUF_4		EQU	31H
DISP_SELECT		EQU	32H
DISP_TEMP		EQU	33H

MENU_FLAG		EQU	34H
MENU_FLAG_TEMP	EQU	35H

CLK_SET_FLAG	EQU	36H
SET_FLAG		EQU	37H
SET_BLINK		EQU	38H
BLINK_CNT		EQU	39H

T_SEC			EQU	3FH
T_1M			EQU	40H
T_10M			EQU	41H
T_1H			EQU	42H
T_10H			EQU	43H

ST_1S			EQU	46H
ST_10S			EQU	47H
ST_1M			EQU	48H
ST_10M			EQU	49H

A_CLR_BIT		EQU	4FH
A_1M			EQU	50H
A_10M			EQU	51H
A_1H			EQU	52H
A_10H			EQU	53H
A_CNT			EQU	54H


D_FLAG			EQU	5EH
D_CNT			EQU	5FH
D_MONTH_2		EQU	60H
D_MONTH_1		EQU	61H
D_DAY_2			EQU	62H
D_DAY_1			EQU	63H

AST_1S			EQU	65H
AST_10S			EQU	66H
AST_1M			EQU	67H
AST_10M			EQU	68H

	ORG	0000
	GOTO	START

	ORG	04H
	MOVWF	W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP
	MOVF 	PCLATH, W
	MOVWF 	PCLATH_TEMP
	CLRF 	PCLATH
	MOVLW 	0FFH  
	MOVWF 	TMR1H
	MOVLW 	0C0H
	MOVWF 	TMR1L	
	CALL	DISP
	CALL	BTN_CHK	
	MOVF	PCLATH_TEMP,W
	MOVWF	PCLATH
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W
	BCF		PIR1, TMR1IF
	RETFIE

CONV
;	CLRF	PCLATH
	ANDLW	0FH			
	ADDWF	PCL,F		
	RETLW	B'11111100'		; '0'
	RETLW	B'01100000'		; '1'
	RETLW	B'11011010'		; '2'
	RETLW	B'11110010'		; '3'
	RETLW	B'01100110'		; '4'
	RETLW	B'10110110'		; '5'
	RETLW	B'10111110'		; '6'
	RETLW	B'11100100'		; '7'
	RETLW	B'11111110'		; '8'
	RETLW	B'11110110'		; '9'
	RETLW	B'00000000'		;공백 	
	RETLW	B'00000010'		; '-'
	RETLW	B'00000000'		; ' '
	RETLW	B'00011010'		; 'C'
	RETLW	B'00000001'		; '.'
	RETLW	B'10011110'		; 'E'
	RETLW	B'10001110'		; 'F'

SELECT_MODE
	CALL	SELECT_DETAIL
	MOVWF	MENU_FLAG_TEMP
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	IORWF	MENU_FLAG_TEMP,W
	MOVWF	MENU_FLAG
	RETURN
SELECT_DETAIL
;	CLRF	PCLATH
	ANDLW	0FH			
	ADDWF	PCL,F	
	RETLW	B'00000001'	;0
	RETLW	B'00010001'	;1
	RETLW	B'00000010'	;2
	RETLW	B'00000100'	;3
	RETLW	B'00001000'	;4
	RETLW	B'00010100'	;5
	RETLW	B'00011000'	;6
	RETLW	B'00110001'	;7
	RETLW	B'01010001'	;8
	RETLW	B'00100010'	;9
	RETLW	B'01000010'	;10	
	RETLW	B'00110100'	;11
	RETLW	B'01010100'	;12
	RETLW	B'00111000'	;13
	RETLW	B'01011000'	;14

	RETLW	B'10001000'	;15	;DDD

BTN_CHK
	MOVF	PORTB,W
	ANDLW	B'00111000'
	SUBLW	B'00111000'
	BTFSC	STATUS,Z
	RETURN		
	MOVLW	.50
	SUBWF	BTNDEL,W
	BTFSS	STATUS,Z
	GOTO	SW_VALID
	CLRF	BTNVALID
	MOVF	PORTB,W
	ANDLW	B'00111000'
	MOVWF	BTNVALID
	GOTO	SW_VALID
	
SW_VALID
	DECFSZ	BTNDEL,F
	RETURN

	MOVLW	.50
	MOVWF	BTNDEL

	MOVF	PORTB,W
	ANDLW	B'00111000'
	SUBWF	BTNVALID,W
	BTFSS	STATUS,Z
	RETURN
	
	MOVF	BTNVALID,W
	SUBLW	B'00110000'	;A
	BTFSC	STATUS,Z
	GOTO	SW_A

	MOVF	BTNVALID,W
	SUBLW	B'00101000'	;B
	BTFSC	STATUS,Z
	GOTO	SW_B

	MOVF	BTNVALID,W
	SUBLW	B'00011000'	;C
	BTFSC	STATUS,Z
	GOTO	SW_C

	MOVF	BTNVALID,W
	SUBLW	B'00001000'	;B+C
	BTFSC	STATUS,Z
	GOTO	SW_BC
	RETURN


SW_A		
	BTFSC	SET_FLAG,7		;AT CAL SET MODE DISABLE/ABLE
	RETURN
	BTFSC	MENU_FLAG,0
	CALL	SW_A_SUB1			;clock OR CLK SET MODE
	BTFSC	MENU_FLAG,1
	GOTO	SW_A_STW_SET		;stw -> clk mode
	BTFSC	MENU_FLAG,2
	CALL	SW_A_SUB2			;calender OR CAL SET
	BTFSC	MENU_FLAG,3
	CALL 	SW_A_SUB3			;alram OR ALRAM SET
	CALL	SELECT_MODE
	RETURN
SW_A_SUB1
	MOVLW	.1				;CLK MODE -> CLK SET
	BTFSC	MENU_FLAG,4			;CLOCK SET -> CLK MODE
	MOVLW	.0
	RETURN
SW_A_SUB2
	MOVLW	.0				;CALENDAR MODE -> CLK MODE
	BTFSC	MENU_FLAG,4			;CAL MODE -> CALENDER SET
	MOVLW	.3
	RETURN
SW_A_SUB3
	MOVLW	.0				;ALR -> CLK
	BTFSC	MENU_FLAG,4			;ALR SET -> ALRAM MODE
	MOVLW	.4
	RETURN
SW_A_STW_SET
	BTFSS	MENU_FLAG,4
	GOTO	SW_A_STW_SUB1
	GOTO	SW_A_STW_SUB2
SW_A_STW_SUB1
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	IORLW	B'00000001'
	MOVWF	MENU_FLAG
	RETURN

SW_A_STW_SUB2
	MOVLW	B'10000010'
	MOVWF	MENU_FLAG
	RETURN
	
SW_B
	BTFSC	MENU_FLAG,0
	CALL	SW_B_SUB1			;clock OR CLK SET OR ALR MODE
	BTFSC	MENU_FLAG,1
	GOTO	SW_B_STW_SET		;stopwatch start/pause
	BTFSC	MENU_FLAG,2
	CALL	SW_B_SUB2			;CAL OR CAL SET
	BTFSC	MENU_FLAG,3
	CALL	SW_B_SUB3			;alram OR alram setting
	CALL	SELECT_MODE
	RETURN
SW_B_SUB1
	MOVLW	.2				;CLK -> STW
	BTFSC	MENU_FLAG,4			;CLOCK SET -> CLK MODE
	MOVLW	.7
	RETURN
SW_B_SUB2
	MOVLW	.5				;CAL -> CALSET
	BTFSC	MENU_FLAG,4			;CALSET -> SELECT DGIT
	MOVLW	.11
	RETURN
SW_B_SUB3
	MOVLW	.6				;ALR -> ALR SET
	BTFSC	MENU_FLAG,4			;ALRSET -> ALR SELECT DGIT
	MOVLW	.13
	RETURN
SW_B_STW_SET
	BTFSS	MENU_FLAG,4
	GOTO	SW_B_STW_SUB1
	GOTO	SW_B_STW_SUB2
SW_B_STW_SUB1
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	MOVLW	B'00100010'
	IORWF	MENU_FLAG,F
	RETURN
SW_B_STW_SUB2
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	MOVLW	B'00110010'
	MOVWF	MENU_FLAG
	RETURN

SW_C
	BTFSC	MENU_FLAG,0
	CALL	SW_C_SUB1			;CLK OR CLK SET
	BTFSC	MENU_FLAG,1
	GOTO	SW_C_STW_SET		;stw -> stw reset
	BTFSC	MENU_FLAG,2
	CALL	SW_C_SUB2			;CAL OR CAL SET
	BTFSC	MENU_FLAG,3
	GOTO	SW_C_SUB3			;ALR OR ALR SET
	CALL	SELECT_MODE
	RETURN
SW_C_SUB1
	MOVLW	.3				;CLK -> CAL
	BTFSC	MENU_FLAG,4			;CLK SET -> INC DGIT
	MOVLW	.8
	RETURN
SW_C_SUB2
	MOVLW	.3				;CAL -> CAL
	BTFSC	MENU_FLAG,4			;CAL SET -> INC DGIT
	MOVLW	.12
	RETURN
SW_C_SUB3
	BTFSS	MENU_FLAG,4
	GOTO	SW_C_ALR_SUB1	;ALR
	GOTO	SW_C_ALR_SUB2	;ARL SET

SW_C_ALR_SUB1	;ALR TOGGLE
	MOVF	MENU_FLAG,W
	XORLW	B'10000000'
	MOVWF	MENU_FLAG
	RETURN

SW_C_ALR_SUB2	;AR SET ->INC DIGT
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	IORLW	B'01011000'
	MOVWF	MENU_FLAG
	RETURN

SW_C_STW_SET
	BTFSS	MENU_FLAG,4
	GOTO	SW_C_STW_SUB1
	GOTO	SW_C_STW_SUB2
SW_C_STW_SUB1
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	IORLW	B'01000010'
	MOVWF	MENU_FLAG
	RETURN
SW_C_STW_SUB2
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	IORLW	B'01010010'
	MOVWF	MENU_FLAG
	RETURN

SW_BC
	BTFSC	MENU_FLAG,1
	GOTO	SW_BC_SUB1
	BTFSS	MENU_FLAG,0
	RETURN
	MOVLW	.4
	CALL	SELECT_MODE
	RETURN
SW_BC_SUB1
	BTFSC	MENU_FLAG,4
	RETURN
	MOVF	MENU_FLAG,W
	ANDLW	B'10000000'
	IORLW	B'00010010'
	MOVWF	MENU_FLAG
	RETURN

DISP
	MOVLW	B'00001100'
	IORWF	PORTA,F
	MOVLW	B'00000110'			
	IORWF	PORTB,F	
	
	BTFSC	DISP_SELECT,3
	GOTO	DISP_4
	BTFSC	DISP_SELECT,2
	GOTO	DISP_3
	BTFSC	DISP_SELECT,1
	GOTO	DISP_2
	GOTO	DISP_1
DISP_4				
;	BSF		PORTA, 3

	MOVF	DISP_BUF_4,W
	CALL	CONV
	BTFSC	SET_BLINK,0
	CALL	BLINK
	CALL	DISP_COM

	BCF		PORTA,3
	BCF		STATUS,C
	RRF		DISP_SELECT,F
	BTFSC	MENU_FLAG,3	;ALR
	BSF		PORTA,0
	RETURN
DISP_3					
;	BSF		PORTB, 1

	MOVF	DISP_BUF_3,W
	CALL	CONV
	BTFSC	SET_BLINK,0
	CALL	BLINK
	CALL	DISP_COM

	BCF		PORTA, 2
	BCF		STATUS,C
	RRF		DISP_SELECT,F
	BTFSC	MENU_FLAG,2	;CALMODE
	BSF		PORTA,0
	RETURN
DISP_2		
;	BSF		PORTB, 2
	
	MOVF	DISP_BUF_2,W
	CALL	CONV
	BTFSC	SET_BLINK,0
	CALL	BLINK
	CALL	DISP_COM

	BCF		PORTB, 2
	BCF		STATUS,C
	RRF		DISP_SELECT,F
	BTFSC	MENU_FLAG,1	;STW MODE
	BSF		PORTA,0
	RETURN
DISP_1		
;	BSF		PORTA, 2		
	MOVF	DISP_BUF_1,W
	CALL	CONV
	BTFSC	SET_BLINK,0
	CALL	BLINK

	CALL	DISP_COM
	BCF		PORTB, 1
	MOVLW	.8
	MOVWF	DISP_SELECT

	BTFSC	MENU_FLAG,0	;CLK MODE
	BSF		PORTA,0

	INCF	INT_CNT,F	
	INCF	BLINK_CNT,F
	BTFSS	INT_EN,0
	RETURN	
	MOVLW	.128		;;MODIFY
	SUBWF	INT_CNT,W
	BTFSS	STATUS,Z
	RETURN	
	INCF	ST_1S,F	
	RETURN

BLINK
;	CLRF	PCLATH	;CAUTION
	MOVWF	DISP_TEMP
	MOVF	DISP_SELECT,W
	SUBWF	CLK_SET_FLAG,W
	BTFSS	STATUS,Z
	GOTO	BLINK_SUB
	MOVLW	B'00000000'
	MOVWF	DISP_TEMP
	RETURN
BLINK_SUB
	MOVF	DISP_TEMP,W
	RETURN

DISP_COM
	MOVWF	DISP_TEMP
	MOVLW	B'00000011'
	ANDWF	PORTC,F
	MOVLW	B'11111100'
	ANDWF	PORTA,F
	
	MOVF	DISP_TEMP,W
	ANDLW	B'11111100'
	IORWF	PORTC,F
	MOVF	DISP_TEMP,W
	ANDLW	B'00000011'
	IORWF	PORTA,F
	RETURN
	

START

	BSF		STATUS, RP0
	MOVLW	B'00000111'
	MOVWF	ADCON1
	MOVLW	00H
	MOVWF	TRISA
	MOVLW	B'00111000'
	MOVWF	TRISB
	MOVLW	B'00000011'
	MOVWF	TRISC
	BSF		PIE1, TMR1IE
	BCF 	OPTION_REG, NOT_RBPU 
	BCF		STATUS, RP0	
	
	BSF		INTCON, GIE	
	BSF		INTCON, PEIE

	MOVLW	B'00001111'
	MOVWF	T1CON
	BSF		PORTA,4		

	GOTO	MAIN_P

MAIN_P
	BSF		PORTA,4		

	MOVLW	T_1M
	MOVWF	FSR

	MOVLW 	0FFH  
	MOVWF 	TMR1H
	MOVLW 	0C0H
	MOVWF 	TMR1L

	CLRF	W_TEMP
	CLRF	STATUS_TEMP		

	CLRF	DISP_BUF_1
	CLRF	DISP_BUF_2
	CLRF	DISP_BUF_3
	CLRF	DISP_BUF_4
	MOVLW	.8
	MOVWF	DISP_SELECT	

	MOVLW	01H
	MOVWF	MENU_FLAG		
	MOVWF	CLK_SET_FLAG	
	CLRF	SET_FLAG		

	MOVLW	.3
	MOVWF	A_CNT
	CLRF	A_CLR_BIT

	CLRF	BTNVALID
	
	MOVLW	.30
	MOVWF	T_SEC
	MOVLW	.4
	MOVWF	T_1M
	MOVLW	.3
	MOVWF	T_10M
	MOVLW	.2
	MOVWF	T_1H
	MOVLW	.2
	MOVWF	T_10H
	
	MOVLW	.1
	MOVWF	D_MONTH_1
	MOVWF	D_DAY_1
	MOVWF	D_CNT
	MOVWF	D_FLAG

	MOVLW	.1
	MOVWF	A_10H
	MOVLW	.2
	MOVWF	A_1H
	MOVLW	.3
	MOVWF	A_10M
	MOVLW	.4
	MOVWF	A_1M
	MOVLW	.50			;modify
	MOVWF	BTNDEL
	CLRF	BLINK_CNT

	MOVLW	.4
	MOVWF	AST_1S
	CLRF	AST_10S
	CLRF	AST_1M
	CLRF	AST_10M
	GOTO	M_LOOP
M_LOOP
	MOVLW	.128		;modify
	SUBWF	INT_CNT,W
	BTFSS	STATUS,Z
	GOTO	CHK_FLAG

	BTFSS	PORTA,4
	DECF	A_CNT,F	;DDDD

	GOTO	ALARM_CHK			
ALARM_CHK
	BTFSS	MENU_FLAG,7	
	GOTO	T_S_LP
;	GOTO	T_ALR
;T_ALR
	BTFSC	A_CLR_BIT,0
	GOTO	T_S_LP

	MOVF	A_10H,W
	SUBWF	T_10H,W
	BTFSS	STATUS,Z
	GOTO	T_S_LP
	MOVF	A_1H,W
	SUBWF	T_1H,W
	BTFSS	STATUS,Z
	GOTO	T_S_LP
	MOVF	A_10M,W
	SUBWF	T_10M,W
	BTFSS	STATUS,Z
	GOTO	T_S_LP
	MOVF	A_1M,W
	SUBWF	T_1M,W
	BTFSS	STATUS,Z
	GOTO	T_S_LP
	GOTO	ALARM_LP
ALARM_LP
	BCF		PORTA,4
	MOVF	A_CNT,W
	SUBLW	.0
	BTFSS	STATUS,Z
	GOTO	T_S_LP
	BSF		PORTA,4
	BSF		A_CLR_BIT,0
	MOVLW	.3
	MOVWF	A_CNT
	GOTO	T_S_LP
T_S_LP
	CLRF	INT_CNT
	INCF	T_SEC,F
	MOVLW	.60
	SUBWF	T_SEC,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	T_1M_LP
T_1M_LP
	BCF		A_CLR_BIT,0
	CLRF	T_SEC
	INCF	T_1M,F
	BCF		STATUS,Z
	MOVLW	.10
	SUBWF	T_1M,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	T_10M_LP
T_10M_LP
	CLRF	T_1M
	INCF	T_10M,F
	BCF		STATUS,Z
	MOVLW	.6
	SUBWF	T_10M,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	T_1H_LP
T_1H_LP
	CLRF	T_10M
	INCF	T_1H,F
	BCF		STATUS,Z
	MOVLW	.10
	SUBWF	T_1H,W
	BTFSC	STATUS,Z
	GOTO	T_10H_LP
	BCF		STATUS,Z
	MOVLW	.2
	SUBWF	T_10H,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	MOVLW	.4
	SUBWF	T_1H,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	DATE_LP
T_10H_LP
	CLRF	T_1H
	INCF	T_10H,F
	GOTO	M_LOOP
DATE_LP	
	BCF		STATUS,Z
	BTFSC	D_FLAG,0
	GOTO	D_DAY_1_LP
	BSF		D_FLAG,0	
	MOVLW	.2
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	BSF		D_FLAG,7
	MOVLW	.1
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	BSF		D_FLAG,5
	MOVLW	.4
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	BSF		D_FLAG,5	
	MOVLW	.6
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	BSF		D_FLAG,5
	MOVLW	.9
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	BSF		D_FLAG,5
	BTFSC	D_FLAG,5
	GOTO	D_30
	BTFSC	D_FLAG,7
	GOTO	D_28
	BSF		D_FLAG,6
	GOTO	D_31
D_28
	BCF		STATUS,Z
	MOVLW	.1
	SUBWF	D_MONTH_2,W
	BTFSC	STATUS,Z
	GOTO	D_31	
	MOVLW	.4
	MOVWF	D_CNT
	GOTO	D_DAY_1_LP
D_30
	BCF		STATUS,Z
	MOVLW	.1
	SUBWF	D_MONTH_2,W
	BTFSC	STATUS,Z
	GOTO	D_31
	MOVLW	.2
	MOVWF	D_CNT
	GOTO	D_DAY_1_LP
D_31
	BCF		STATUS,Z
	MOVLW	.1
	MOVWF	D_CNT
	GOTO	D_DAY_1_LP
D_DAY_1_LP
	CLRF	T_10H
	CLRF	T_1H
	INCF	D_CNT,F	
	MOVLW	.32
	SUBWF	D_CNT,W
	BTFSC	STATUS,Z
	GOTO	D_MONTH_1_LP

	INCF	D_DAY_1,F
	BCF		STATUS,Z
	MOVLW	.10
	SUBWF	D_DAY_1,W
	BTFSC	STATUS,Z
	GOTO	D_DAY_2_LP
	GOTO	M_LOOP
D_DAY_2_LP
	CLRF	D_DAY_1
	INCF	D_DAY_2,F
	GOTO	M_LOOP

D_MONTH_1_LP
	CLRF	D_DAY_1
	CLRF	D_DAY_2
	CLRF	D_CNT
	BCF		D_FLAG,0
	BCF		D_FLAG,7
	BCF		D_FLAG,6
	BCF		D_FLAG,5
	INCF	D_MONTH_1,F
	BCF		STATUS,Z
	MOVLW	.10
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	GOTO	D_MONTH_2_LP
	MOVLW	.1
	SUBWF	D_MONTH_2,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	MOVLW	.3
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	GOTO	D_MONTH_CLEAR
D_INIT
	MOVLW	.1
	MOVWF	D_DAY_1
	GOTO	M_LOOP
D_MONTH_2_LP
	CLRF	D_MONTH_1
	INCF	D_MONTH_2,F
	BCF		STATUS,Z
	GOTO	D_INIT
D_MONTH_CLEAR
	CLRF	D_MONTH_2
	MOVLW	.1
	MOVWF	D_MONTH_1
	GOTO	D_INIT

CHK_FLAG
	BTFSC	MENU_FLAG,0
	GOTO	CLK_MODE		;CLK MODE OR SETTING
	BTFSC	MENU_FLAG,1
	GOTO	STOPW_MODE		;SW MODE
	BTFSC	MENU_FLAG,2
	GOTO	CAL_MODE		;CAL MODE OR SETTING
	BTFSC	MENU_FLAG,3
	GOTO	ALR_MODE		;ALRAM MODE	OR SETTING
	GOTO	MAIN_P			;ERROR-RESET

CLK_MODE
	BTFSC	MENU_FLAG,4
	GOTO	SETTING_MODE_1	

	CLRF	SET_FLAG
	CLRF	SET_BLINK
	MOVLW	.1
	MOVWF	CLK_SET_FLAG

	MOVF	T_1M,W
	MOVWF	DISP_BUF_1

	MOVF	T_10M,W
	MOVWF	DISP_BUF_2

	MOVF	T_1H,W
	MOVWF	DISP_BUF_3

	MOVF	T_10H,W
	MOVWF	DISP_BUF_4

	GOTO	M_LOOP	;T_1S_LP

STOPW_MODE

	BTFSC	MENU_FLAG,4
	GOTO	SETTING_MODE_4
	BTFSC	MENU_FLAG,6	
	GOTO	STOP_RESET

	BTFSC	MENU_FLAG,5	
	GOTO	STOP_START	

	CLRF	SET_FLAG
	CLRF	SET_BLINK

	MOVLW	.1
	MOVWF	CLK_SET_FLAG

	MOVF	ST_1S,W
	MOVWF	DISP_BUF_1

	MOVF	ST_10S,W
	MOVWF	DISP_BUF_2

	MOVF	ST_1M,W
	MOVWF	DISP_BUF_3

	MOVF	ST_10M,W
	MOVWF	DISP_BUF_4

	GOTO	ST_ALR
STOP_START		
	MOVLW	.2
	BCF		MENU_FLAG,5
	CALL	SELECT_MODE
	MOVWF	MENU_FLAG

	MOVLW	B'00000001'
	XORWF	INT_EN,F
	GOTO	M_LOOP
STOP_RESET
	MOVLW	.2
	CALL	SELECT_MODE
	MOVWF	MENU_FLAG
	CLRF	ST_1S
	CLRF	ST_10S
	CLRF	ST_1M
	CLRF	ST_10M
	BCF		INT_EN,0
	GOTO	M_LOOP
;----STWCOUNT-----------
ST_ALR
	;BTFSC	A_CLR_BIT,1
	;GOTO	ST_1S_LP
	BTFSS	PORTA,4
	GOTO	ST_ALARM_LP

	BTFSS	MENU_FLAG,7
	GOTO	ST_1S_LP
	MOVF	AST_10M,W
	SUBWF	ST_10M,W
	BTFSS	STATUS,Z
	GOTO	ST_1S_LP
	MOVF	AST_1M,W
	SUBWF	ST_1M,W
	BTFSS	STATUS,Z
	GOTO	ST_1S_LP
	MOVF	AST_10S,W
	SUBWF	ST_10S,W
	BTFSS	STATUS,Z
	GOTO	ST_1S_LP
	MOVF	AST_1S,W
	SUBWF	ST_1S,W
	BTFSS	STATUS,Z
	GOTO	ST_1S_LP
	GOTO	ST_ALARM_LP

ST_ALARM_LP
	BCF		INT_EN,0
	BCF		PORTA,4
	MOVLW	.0
	SUBWF	A_CNT,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	BSF		PORTA,4
	MOVLW	.3
	MOVWF	A_CNT
	MOVLW	B'00000010'
	MOVWF	MENU_FLAG
	GOTO	STOP_RESET

ST_1S_LP
	MOVLW	.10
	SUBWF	ST_1S,W		;W=F-W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	ST_10S_LP
ST_10S_LP
	CLRF	ST_1S
	INCF	ST_10S,F
	BCF		STATUS,Z
	MOVLW	.6
	SUBWF	ST_10S,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	ST_1M_LP
ST_1M_LP
	CLRF	ST_10S
	INCF	ST_1M,F
	BCF		STATUS,Z
	MOVLW	.10
	SUBWF	ST_1M,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	GOTO	ST_10M_LP
ST_10M_LP
	CLRF	ST_1M
	INCF	ST_10M,F
	BCF		STATUS,Z
	MOVLW	.6
	SUBWF	ST_10M,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	ST_10M
	GOTO	M_LOOP

CAL_MODE
	BTFSC	MENU_FLAG,4
	GOTO	SETTING_MODE_2	;CALENDAR SETTING MODE
	CLRF	SET_FLAG
	CLRF	SET_BLINK
	MOVLW	.1
	MOVWF	CLK_SET_FLAG

	MOVF	DISP_BUF_1,W
	ANDLW	B'11110000'
	IORWF	D_DAY_1,W
	MOVWF	DISP_BUF_1

	MOVF	DISP_BUF_2,W
	ANDLW	B'11110000'
	IORWF	D_DAY_2,W
	MOVWF	DISP_BUF_2

	MOVF	DISP_BUF_3,W
	ANDLW	B'11110000'
	IORWF	D_MONTH_1,W
	MOVWF	DISP_BUF_3

	MOVF	DISP_BUF_4,W
	ANDLW	B'11110000'
	IORWF	D_MONTH_2,W
	MOVWF	DISP_BUF_4
	GOTO	M_LOOP

;-----------------------------------------------------------------------
;----alram mode
ALR_MODE
	BTFSC	MENU_FLAG,4
	GOTO	SETTING_MODE_3	;ALR SETTING
	BTFSC	MENU_FLAG,6
	GOTO	ALR_TOGGLE		;ALR ON/OFF
	
	CLRF	SET_FLAG
	CLRF	SET_BLINK
	MOVLW	.1
	MOVWF	CLK_SET_FLAG

	MOVF	A_1M,W
	MOVWF	DISP_BUF_1

	MOVF	A_10M,W
	MOVWF	DISP_BUF_2

	MOVF	A_1H,W
	MOVWF	DISP_BUF_3

	MOVF	A_10H,W
	MOVWF	DISP_BUF_4

	GOTO	M_LOOP
ALR_TOGGLE	;DOT 표시...?
	BCF		MENU_FLAG,6
	MOVLW	B'10000000'
	XORWF	MENU_FLAG,F
	GOTO	ALR_MODE



;----------------------------------------------------------------------
;====setting mode_1 : TIME SET
SETTING_MODE_1	
	BTFSC	SET_FLAG,0
	GOTO	SETTIME_1
	BSF		SET_FLAG,0
	MOVLW	T_1M
	MOVWF	FSR
;	BSF		SET_BLINK,0	;DDD
	MOVLW	.1
	MOVWF	CLK_SET_FLAG

SETTIME_1
;setting disply
;blink select port
;BIT 하나정해서 한번은 걍넣고
;한번은 해당넘버 공란으로.. 절반속도로
	MOVF	T_1M,W
	MOVWF	DISP_BUF_1

	MOVF	T_10M,W
	MOVWF	DISP_BUF_2

	MOVF	T_1H,W
	MOVWF	DISP_BUF_3

	MOVF	T_10H,W
	MOVWF	DISP_BUF_4
	
	MOVLW	.32		;MODIFY
	SUBWF	BLINK_CNT,W
	BTFSS	STATUS,Z
	GOTO	SETTIME_BLINK
	CLRF	BLINK_CNT
	MOVLW	B'00000001'
	XORWF	SET_BLINK,F	;SET BLINK TOGGLE
	GOTO	SETTIME_BLINK
SETTIME_BLINK
	BTFSC	MENU_FLAG,5	;select dgit
	CALL	SETTING_SELECT

	BTFSC	MENU_FLAG,6	;inc dgit
	GOTO	INC_DIGIT
	GOTO	M_LOOP
SETTING_SELECT	;깜빡+변경
	BCF		MENU_FLAG,5
	INCF	FSR,F
	RLF		CLK_SET_FLAG,F
	BTFSS	CLK_SET_FLAG,4
	RETURN
	MOVLW	T_1M
	MOVWF	FSR
	MOVLW	.1
	MOVWF	CLK_SET_FLAG
	RETURN

INC_DIGIT ; 0~9, 0~5, 0~2
	BCF		MENU_FLAG,6
	BTFSC	CLK_SET_FLAG,0;1분
	GOTO	INC_10
	BTFSC	CLK_SET_FLAG,1;10분
	GOTO	INC_6
	BTFSC	CLK_SET_FLAG,2;1시
	GOTO	INC_10
	BTFSC	CLK_SET_FLAG,3;10시
	GOTO	INC_3
	GOTO	M_LOOP
INC_10 ;0~9
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.10
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_6 ;0~5
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.6
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_3 ;0~2
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.3
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP


;====setting mode_2 : DATE
SETTING_MODE_2
	BTFSC	SET_FLAG,1
	GOTO	SETDATE_1
	BSF		SET_FLAG,1
	MOVLW	D_MONTH_2
	MOVWF	FSR
	MOVLW	.8
	MOVWF	CLK_SET_FLAG
;	BSF		SET_BLINK,0	;DDD

SETDATE_1
	MOVF	D_DAY_1,W
	MOVWF	DISP_BUF_1

	MOVF	D_DAY_2,W
	MOVWF	DISP_BUF_2

	MOVF	D_MONTH_1,W
	MOVWF	DISP_BUF_3

	MOVF	D_MONTH_2,W
	MOVWF	DISP_BUF_4
	
	MOVLW	.32		;MODIFY
	SUBWF	BLINK_CNT,W
	BTFSS	STATUS,Z
	GOTO	SETDATE_BLINK
	CLRF	BLINK_CNT
	MOVLW	B'00000001'
	XORWF	SET_BLINK	;SET BLINK TOGGLE
	GOTO	SETDATE_BLINK
SETDATE_BLINK
	BTFSC	MENU_FLAG,5	;select dgit
	CALL	SETTING_SELECT_2

	BTFSC	MENU_FLAG,6	;inc dgit
	GOTO	INC_DIGIT_2
	GOTO	M_LOOP

SETTING_SELECT_2	;깜빡+변경
	BSF		SET_FLAG,7	;SW_A DISABLED
	BCF		MENU_FLAG,5
	INCF	FSR,F
	RRF		CLK_SET_FLAG,F
;DDD
;	BTFSC	CLK_SET_FLAG,2;1달
;	CLRF	D_MONTH_1
;	BTFSC	CLK_SET_FLAG,1;10일
;	CLRF	D_DAY_2
;	BTFSC	CLK_SET_FLAG,0;1일
;	CLRF	D_DAY_1

	BTFSS	STATUS,C
	RETURN
	MOVLW	D_MONTH_2
	MOVWF	FSR
	MOVLW	.8
	MOVWF	CLK_SET_FLAG
	BCF		SET_FLAG,7	;SW_A ABLED
	RETURN

INC_DIGIT_2 ;
	BCF		MENU_FLAG,6
	BTFSC	CLK_SET_FLAG,3;10달
	GOTO	INC_3M
	BTFSC	CLK_SET_FLAG,2;1달
	GOTO	INC_10M
	BTFSC	CLK_SET_FLAG,1;10일
	GOTO	INC_3D
	BTFSC	CLK_SET_FLAG,0;1일
	GOTO	INC_10D
	GOTO	M_LOOP
INC_3M ;0~2 10자리 달
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.2
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	MOVLW	.1
	MOVWF	D_MONTH_1
	GOTO	M_LOOP
INC_10M ;0~9, 0~2 1자리 달
	INCF	INDF,F
	MOVLW	.1
	SUBWF	D_MONTH_2,W
	BTFSC	STATUS,Z
	GOTO	INC_10M_SUB_1	;10 11 12
	MOVF	INDF,W
	SUBLW	.10	
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	MOVLW	.1
	MOVWF	D_MONTH_1
	GOTO	M_LOOP
INC_10M_SUB_1	;1X 월 인 경우
	MOVF	INDF,W
	SUBLW	.3
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF	;13 -> 0
	GOTO	M_LOOP
INC_3D ;0~2 10자리 일
	INCF	INDF,F
	MOVLW	.0
	SUBWF	D_MONTH_2,W
	BTFSS	STATUS,Z
	GOTO	INC_3D_SUB	;1X
	MOVLW	.2
	SUBWF	D_MONTH_1,W
	BTFSS	STATUS,Z
	GOTO	INC_3D_SUB	;NOT 02
	MOVF	INDF,W	;02
	SUBLW	.3	;DDD
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	MOVLW	.1
	MOVWF	D_DAY_1
	GOTO	M_LOOP
INC_3D_SUB
	MOVLW	.4
	SUBWF	INDF,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	MOVLW	.1
	MOVWF	D_DAY_1
	GOTO	M_LOOP
INC_10D ;0~9	1자리 일	
	INCF	INDF,F
	MOVLW	.2	
	SUBWF	D_MONTH_1,W
	BTFSS	STATUS,Z
	GOTO	INC_D_SUBCHK	;NOT FEB.
	MOVLW	.1
	SUBWF	D_MONTH_2,W
	BTFSC	STATUS,Z
	GOTO	INC_10D_31DAYS
	GOTO	INC_10D_28DAYS
INC_D_SUBCHK
	MOVLW 	.1
	SUBWF	D_MONTH_1,W
	BTFSS	STATUS,Z
	GOTO	INC_D_NEXT	
	MOVLW	.1
	SUBWF	D_MONTH_2,W
	BTFSC	STATUS,Z
	GOTO	INC_10D_30DAYS	;NOV. ELSE OCT,DEC,JAN
INC_D_NEXT
	MOVLW	.4
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	GOTO	INC_10D_30DAYS
	MOVLW	.6
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	GOTO	INC_10D_30DAYS
	MOVLW	.9
	SUBWF	D_MONTH_1,W
	BTFSC	STATUS,Z
	GOTO	INC_10D_30DAYS
	GOTO	INC_10D_31DAYS
INC_10D_28DAYS
	MOVLW	.2
	SUBWF	D_DAY_2,W
	BTFSS	STATUS,Z
	GOTO	INC_10D_SUB1
	MOVF	INDF,W
	SUBLW	.9
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_10D_30DAYS
	MOVLW	.3
	SUBWF	D_DAY_2,W
	BTFSS	STATUS,Z
	GOTO	INC_10D_SUB1
	CLRF	INDF
	MOVLW	.1
	MOVWF	D_DAY_1
	GOTO	M_LOOP
INC_10D_31DAYS
	MOVLW	.3	
	SUBWF	D_DAY_2,W
	BTFSS	STATUS,Z
	GOTO	INC_10D_SUB1
	MOVF	INDF,W
	SUBLW	.2
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_10D_SUB1
	MOVF	INDF,W
	SUBLW	.10
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	MOVLW	.0
	SUBWF	D_DAY_2,W
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	MOVLW	.1
	MOVWF	INDF
	GOTO	M_LOOP

SETTING_MODE_3

	BTFSC	SET_FLAG,2
	GOTO	SETALARM_1
	BSF		SET_FLAG,2
	MOVLW	A_1M
	MOVWF	FSR
;	BSF		SET_BLINK,0	;DDD
	MOVLW	.1
	MOVWF	CLK_SET_FLAG

SETALARM_1
	MOVF	A_1M,W
	MOVWF	DISP_BUF_1

	MOVF	A_10M,W
	MOVWF	DISP_BUF_2

	MOVF	A_1H,W
	MOVWF	DISP_BUF_3

	MOVF	A_10H,W
	MOVWF	DISP_BUF_4
	
	MOVLW	.32		;MODIFY
	SUBWF	BLINK_CNT,W
	BTFSS	STATUS,Z
	GOTO	SETALR_BLINK
	CLRF	BLINK_CNT
	MOVLW	B'00000001'
	XORWF	SET_BLINK	;SET BLINK TOGGLE
	GOTO	SETALR_BLINK
SETALR_BLINK
	BTFSC	MENU_FLAG,5	;select dgit
	CALL	SETTING_SELECT_3
	
	BTFSC	MENU_FLAG,6	;inc dgit
	GOTO	INC_DIGIT_3
	GOTO	M_LOOP
SETTING_SELECT_3	;깜빡+변경
	BCF		MENU_FLAG,5
	INCF	FSR,F
	RLF		CLK_SET_FLAG,F
	BTFSS	CLK_SET_FLAG,4
	RETURN
	MOVLW	A_1M
	MOVWF	FSR
	MOVLW	.1
	MOVWF	CLK_SET_FLAG
	RETURN

INC_DIGIT_3 ; 0~9, 0~5, 0~2
	BCF		MENU_FLAG,6
	BTFSC	CLK_SET_FLAG,0;1분
	GOTO	INC_10_A
	BTFSC	CLK_SET_FLAG,1;10분
	GOTO	INC_6_A
	BTFSC	CLK_SET_FLAG,2;1시
	GOTO	INC_10_A
	BTFSC	CLK_SET_FLAG,3;10시
	GOTO	INC_3_A
	GOTO	M_LOOP
INC_10_A ;0~9
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.10
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_6_A ;0~5
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.6
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_3_A ;0~2
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.2
	BTFSC	STATUS,Z
	CLRF	A_1H
	MOVF	INDF,W
	SUBLW	.3
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP

SETTING_MODE_4
	BTFSC	SET_FLAG,0
	GOTO	SETSTW_1
	BSF		SET_FLAG,0
	MOVLW	AST_1S
	MOVWF	FSR
;	BSF		SET_BLINK,0	;DDD
	MOVLW	.1
	MOVWF	CLK_SET_FLAG

SETSTW_1
	MOVF	AST_1S,W
	MOVWF	DISP_BUF_1

	MOVF	AST_10S,W
	MOVWF	DISP_BUF_2

	MOVF	AST_1M,W
	MOVWF	DISP_BUF_3

	MOVF	AST_10M,W
	MOVWF	DISP_BUF_4
	
	MOVLW	.32		;MODIFY
	SUBWF	BLINK_CNT,W
	BTFSS	STATUS,Z
	GOTO	SETSTW_BLINK
	CLRF	BLINK_CNT
	MOVLW	B'00000001'
	XORWF	SET_BLINK	;SET BLINK TOGGLE
	GOTO	SETSTW_BLINK
SETSTW_BLINK
	BTFSC	MENU_FLAG,5	;select dgit
	CALL	SELECT_STW

	BTFSC	MENU_FLAG,6	;inc dgit
	GOTO	INC_STW
	GOTO	M_LOOP
SELECT_STW	;깜빡+변경
	BCF		MENU_FLAG,5
	INCF	FSR,F
	RLF		CLK_SET_FLAG,F
	BTFSS	CLK_SET_FLAG,4
	RETURN
	MOVLW	AST_1S
	MOVWF	FSR
	MOVLW	.1
	MOVWF	CLK_SET_FLAG
	RETURN

INC_STW
; 0~9, 0~5, 0~2
	BCF		MENU_FLAG,6
	BTFSC	CLK_SET_FLAG,0;1S
	GOTO	INC_10ST
	BTFSC	CLK_SET_FLAG,1;10S
	GOTO	INC_6ST
	BTFSC	CLK_SET_FLAG,2;1M
	GOTO	INC_10ST
	BTFSC	CLK_SET_FLAG,3;10M
	GOTO	INC_6ST
	GOTO	M_LOOP
INC_10ST ;0~9
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.10
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP
INC_6ST ;0~5
	INCF	INDF,F
	MOVF	INDF,W
	SUBLW	.6
	BTFSS	STATUS,Z
	GOTO	M_LOOP
	CLRF	INDF
	GOTO	M_LOOP

	END